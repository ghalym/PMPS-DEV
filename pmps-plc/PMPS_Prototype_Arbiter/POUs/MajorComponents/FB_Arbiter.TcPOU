<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.12">
  <POU Name="FB_Arbiter" Id="{9cc1f6b7-c43c-0fbb-09be-271b21ce1be7}" SpecialFunc="None">
    <Declaration><![CDATA[(* FB Arbiter

The arbiter primary objectives are:
- Provide a simple interface for devices to request beam parameter sets
- Provide a way for devices to verify their BPS is active in the arbiter
- Provide a way for devices remove their requests from evaluation
- Evaluate all active beam parameter requests registered with the aribiter,
to determine the safest combination of all sets, provide this set as an output.
- Do all of this with minimal overhead

To these ends, the arbiter uses a hash-table, the rows being a state-id as the key, and a corresponding
 beam parameter set to be evaluated against all the other sets (or rows), in the table.
 
The hash table can be thought of as an array on steriods, they are worth reading about, suffice to say
the hash table will tell you when you reach the end of all the entries, and enables us to find entries quickly.

These features efficiently address the addition, removal, and verification of beam parameter sets listed in the above requirements.

An important thing to pay attention to is the A_Arbitrate action. This action implements the evaluation of beam parameter
sets against each other.

*)

FUNCTION_BLOCK FB_Arbiter
VAR_INPUT
	i_nStateID	:	UDINT; //Set to the stateID of the state requesting the beam parameter set
	i_stAssertBeamParams	:	ST_BeamParams; //Set to the beam parameter set of the state required
END_VAR
VAR_OUTPUT
	q_stBeamParams	:	ST_BeamParams; //Updated on each cycle of the arbiter FB with the current arbitrated beam parameter set
	
	q_xStateIDFound	:	BOOL; //Set true if a state-id is found in the assertion pool after calling A_VerifyAssertion
END_VAR
VAR
	
	fbBPAssertionPool	:	FB_BeamParamAssertionPool; //Table of active beam parameter assertions
	
	getPosPtr	: POINTER TO T_HashTableEntry := 0; 
	getBPStruct	:	ST_BeamParams;
	stOutputBP	:	ST_BeamParams; //Safest beam parameters from all in the assertion pool
	
	
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[//Arbitrate
(*
This step cycles through every hash table entry, comparing
the beam parameters from each assertion to what we're planning
to assert.

The safer of the two parameters is kept so at the end we may
have a mix of beam parameters, the composition being safe for
all asserters.

*)

//Safest parameters to start
stOutputBP := F_SetBeamParams(
	fAtt := 100,
	fPP_mJ := 0,
	neVReqRange	:= 0000_0000_0000_0000,
	nRate := 0
	);

fbBPAssertionPool.A_GetFirst( putPosPtr := 0, getPosPtr=>getPosPtr, getValue=>getBPStruct );
IF fbBPAssertionPool.bOk THEN
	
	//The first entry in the hash table is taken as the setting to arbitrate against
	stOutputBP := F_SetBeamParams(
		fAtt := getBPStruct.fAtt,
		fPP_mJ := getBPStruct.fPP_mJ,
		neVReqRange	:= getBPStruct.neVReqRange,
		nRate := getBPStruct.nRate
	);

	REPEAT
		fbBPAssertionPool.A_GetNext( putPosPtr := fbBPAssertionPool.getPosPtr , getPosPtr=>getPosPtr, getValue=>getBPStruct );
		IF fbBPAssertionPool.bOk THEN
			A_Arbitrate();
		END_IF
	UNTIL NOT fbBPAssertionPool.bOk
	END_REPEAT

END_IF

q_stBeamParams := stOutputBP;]]></ST>
    </Implementation>
    <Action Name="A_Arbitrate" Id="{cd232571-a3df-0041-3bcc-1fe44a9b9a88}">
      <Implementation>
        <ST><![CDATA[
//Attenuation
//<TODO>Add something here to register the key that won for each param
stOutputBP.fAtt := MAX(stOutputBP.fAtt, getBPStruct.fAtt);

//Pulse energy
stOutputBP.fPP_mJ := MIN(stOutputBP.fPP_mJ, getBPStruct.fPP_mJ);

//Photon energy ranges
stOutputBP.neVReqRange := stOutputBP.neVReqRange AND getBPStruct.neVReqRange;

//Rate
stOutputBP.nRate := MIN(stOutputBP.nRate, getBPStruct.nRate);]]></ST>
      </Implementation>
    </Action>
    <Action Name="A_AssertBP" Id="{f6e89361-055c-0560-0933-bcdd7d87f7bb}">
      <Implementation>
        <ST><![CDATA[//fbTable.A_Add( key := 12345, putValue := putValue, getPosPtr=>getPosPtr, getValue=>getValue );

(* This action adds a state-id and beam parameter set to the beam parameter assertion pool (arbiter
hash table). Use like so:

fbArbiter.A_AssertBP(
i_nStateID := nStateID,
i_stAssertBeamParams := stDesiredBeamParams
);

*)

IF i_nStateID <> EXCLUDED_ASSERTION_ID THEN

	fbBPAssertionPool.A_Add(
		key := i_nStateID,
		putValue := i_stAssertBeamParams,
		getPosPtr=>getPosPtr,
		getValue => getBPStruct
	);
END_IF
	
IF NOT fbBPAssertionPool.bOk THEN
	; //Overflow <TODO>
END_IF]]></ST>
      </Implementation>
    </Action>
    <Action Name="A_Init" Id="{31a8de0d-caf8-01fc-1b86-c0ff1708d44b}">
      <Implementation>
        <ST><![CDATA[// Clears the entire beam parameter assertion pool (hashtable).

fbBPAssertionPool.A_Reset();]]></ST>
      </Implementation>
    </Action>
    <Action Name="A_RemoveAssertion" Id="{024321c3-bc6f-0692-0562-0a65dc09776d}">
      <Implementation>
        <ST><![CDATA[
(* Searches for a state-id, and removes it from the assertion pool.

Use like so:
fbArbiter.A_RemoveAssertion(
i_nStateId := nStateIDAssertionToRemove
);

*)

IF i_nStateId <> EXCLUDED_ASSERTION_ID THEN
	fbBPAssertionPool.A_Remove(key := i_nStateId, getPosPtr=>getPosPtr, getValue=>getBPStruct);
	
	IF fbBPAssertionPool.bOk THEN
		;
	ELSE
		;
	END_IF
END_IF]]></ST>
      </Implementation>
    </Action>
    <Action Name="A_VerifyAssertion" Id="{905e28be-7804-0837-3111-bddd3e35d311}">
      <Implementation>
        <ST><![CDATA[
(* Searches state assertion pool for a state-id, and sets q_xStateIDFound true if found.

Use like so:

fbArbiter.A_VerifyAssertion(
i_nStateId := nStateIDAssertionToCheck
);

IF fbArbiter.q_xStateIDFound THEN:
	Do something
ELSE:
	Don't do something

*)

IF i_nStateID <> EXCLUDED_ASSERTION_ID THEN
	
	fbBPAssertionPool.A_Lookup( key := i_nStateID, getPosPtr=>getPosPtr, getValue=>getBPStruct );

	q_xStateIDFound	:= fbBPAssertionPool.bOk;

ELSE
	q_xStateIDFound := FALSE;
END_IF]]></ST>
      </Implementation>
    </Action>
    <Method Name="AddRequest" Id="{1e467c4a-5c4c-0f06-2179-99208c80fe24}">
      <Declaration><![CDATA[METHOD AddRequest : BOOL
VAR_INPUT
	nStateID	:	DWORD; //StateID of state requesting beam parameter set
	stReqBP		:	ST_BeamParams;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF nStateID <> EXCLUDED_ASSERTION_ID THEN
	THIS^.fbBPAssertionPool.A_Add(
		key := nStateID,
		putValue := stReqBP
	);
END_IF
	
IF NOT THIS^.fbBPAssertionPool.bOk THEN
	; //Overflow <TODO>
END_IF

AddRequest := THIS^.fbBPAssertionPool.bOk;]]></ST>
      </Implementation>
    </Method>
    <Method Name="ArbitrateBP" Id="{492794c7-4e84-09b8-3bed-5a488badb9f1}">
      <Declaration><![CDATA[METHOD ArbitrateBP : ST_BeamParams
VAR_INPUT
	stBP1	:	ST_BeamParams;
	stBP2	:	ST_BeamParams;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[//<TODO>Add something here to register the key that won for each param

//Attenuation
ArbitrateBP.fAtt := MAX(stBP1.fAtt, stBP2.fAtt);

//Pulse energy
ArbitrateBP.fPP_mJ := MIN(stBP1.fPP_mJ, stBP2.fPP_mJ);

//Photon energy ranges
ArbitrateBP.neVReqRange := stBP1.neVReqRange AND stBP2.neVReqRange;

//Rate
ArbitrateBP.nRate := MIN(stBP1.nRate, stBP2.nRate);]]></ST>
      </Implementation>
    </Method>
    <Method Name="CheckRequest" Id="{4eeb385e-4a95-0260-0556-e5cedcc277ec}">
      <Declaration><![CDATA[(* Searches state assertion pool for a state-id, and sets q_xStateIDFound true if found.

Use like so:

fbArbiter.A_VerifyAssertion(
i_nStateId := nStateIDAssertionToCheck
);

IF fbArbiter.q_xStateIDFound THEN:
	Do something
ELSE:
	Don't do something

*)
METHOD CheckRequest : BOOL
VAR_INPUT
	nReqID	:	DWORD;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[THIS^.fbBPAssertionPool.A_Lookup(key := nReqID);

CheckRequest := THIS^.fbBPAssertionPool.bOk;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetArbitratedBP" Id="{42c0af72-5a4d-02e2-043b-0505463fd885}">
      <Declaration><![CDATA[METHOD GetArbitratedBP : ST_BeamParams
VAR_INPUT
END_VAR
VAR
	getPosPtr	: POINTER TO T_HashTableEntry := 0;
	getBPStruct	:	ST_BeamParams;
	
	stOutputBP	:	ST_BeamParams; //Holding struct for arbitration process
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[//Arbitrate
(*
This step cycles through every hash table entry, comparing
the beam parameters from each assertion to what we're planning
to assert.

The safer of the two parameters is kept so at the end we may
have a mix of beam parameters, the composition being safe for
all asserters.

*)

THIS^.fbBPAssertionPool.A_GetFirst( putPosPtr := 0, getPosPtr=>getPosPtr, getValue=>getBPStruct );
IF THIS^.fbBPAssertionPool.bOk THEN //If the table is empty (not OK), we are left with the default stOutputBP set above.
	
	//The first entry in the hash table is taken as the setting to arbitrate against
	stOutputBP := F_SetBeamParams(
		fAtt := getBPStruct.fAtt,
		fPP_mJ := getBPStruct.fPP_mJ,
		neVReqRange	:= getBPStruct.neVReqRange,
		nRate := getBPStruct.nRate
	);

	REPEAT
		THIS^.fbBPAssertionPool.A_GetNext( putPosPtr := THIS^.fbBPAssertionPool.getPosPtr, getPosPtr=>getPosPtr, getValue=>getBPStruct );
		IF THIS^.fbBPAssertionPool.bOk THEN
			stOutputBP := ArbitrateBP(stOutputBP, getBPStruct);
		END_IF
	UNTIL NOT fbBPAssertionPool.bOk
	END_REPEAT
ELSE
	//Safest parameters to start
	stOutputBP := PMPS_GVL_BeamParams.cstSafeBeam;
END_IF

GetArbitratedBP := stOutputBP;]]></ST>
      </Implementation>
    </Method>
    <Method Name="RemoveRequest" Id="{91259d93-276a-0acc-0f44-27a89e3b464d}">
      <Declaration><![CDATA[(* Searches for a state-id, and removes it from the assertion pool.

Use like so:
fbArbiter.A_RemoveAssertion(
i_nStateId := nStateIDAssertionToRemove
);

*)
METHOD RemoveRequest : BOOL
VAR_INPUT
	nReqId	:	DWORD;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF nReqId <> EXCLUDED_ASSERTION_ID THEN
	THIS^.fbBPAssertionPool.A_Remove(key := nReqId);
	
	IF THIS^.fbBPAssertionPool.bOk THEN
		;
	ELSE
		;
	END_IF
END_IF

RemoveRequest := THIS^.fbBPAssertionPool.bOk;]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_Arbiter">
      <LineId Id="18" Count="16" />
      <LineId Id="77" Count="0" />
      <LineId Id="36" Count="9" />
      <LineId Id="78" Count="0" />
      <LineId Id="47" Count="9" />
      <LineId Id="9" Count="0" />
      <LineId Id="59" Count="0" />
      <LineId Id="58" Count="0" />
    </LineIds>
    <LineIds Name="FB_Arbiter.A_Arbitrate">
      <LineId Id="2" Count="0" />
      <LineId Id="1" Count="0" />
      <LineId Id="6" Count="0" />
      <LineId Id="3" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="4" Count="0" />
      <LineId Id="7" Count="0" />
      <LineId Id="13" Count="0" />
      <LineId Id="9" Count="0" />
      <LineId Id="15" Count="0" />
      <LineId Id="12" Count="0" />
      <LineId Id="11" Count="0" />
      <LineId Id="14" Count="0" />
    </LineIds>
    <LineIds Name="FB_Arbiter.A_AssertBP">
      <LineId Id="2" Count="0" />
      <LineId Id="17" Count="2" />
      <LineId Id="25" Count="0" />
      <LineId Id="23" Count="0" />
      <LineId Id="26" Count="2" />
      <LineId Id="24" Count="0" />
      <LineId Id="22" Count="0" />
      <LineId Id="13" Count="1" />
      <LineId Id="12" Count="0" />
      <LineId Id="1" Count="0" />
      <LineId Id="3" Count="4" />
      <LineId Id="15" Count="1" />
      <LineId Id="8" Count="0" />
      <LineId Id="10" Count="1" />
    </LineIds>
    <LineIds Name="FB_Arbiter.A_Init">
      <LineId Id="2" Count="1" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_Arbiter.A_RemoveAssertion">
      <LineId Id="11" Count="1" />
      <LineId Id="14" Count="0" />
      <LineId Id="13" Count="0" />
      <LineId Id="15" Count="2" />
      <LineId Id="19" Count="0" />
      <LineId Id="18" Count="0" />
      <LineId Id="9" Count="0" />
      <LineId Id="2" Count="0" />
      <LineId Id="1" Count="0" />
      <LineId Id="4" Count="0" />
      <LineId Id="3" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="7" Count="1" />
      <LineId Id="6" Count="0" />
      <LineId Id="10" Count="0" />
    </LineIds>
    <LineIds Name="FB_Arbiter.A_VerifyAssertion">
      <LineId Id="15" Count="3" />
      <LineId Id="28" Count="0" />
      <LineId Id="19" Count="2" />
      <LineId Id="24" Count="0" />
      <LineId Id="23" Count="0" />
      <LineId Id="25" Count="2" />
      <LineId Id="22" Count="0" />
      <LineId Id="9" Count="1" />
      <LineId Id="2" Count="0" />
      <LineId Id="14" Count="0" />
      <LineId Id="1" Count="0" />
      <LineId Id="3" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="11" Count="0" />
      <LineId Id="6" Count="0" />
      <LineId Id="12" Count="1" />
    </LineIds>
    <LineIds Name="FB_Arbiter.AddRequest">
      <LineId Id="27" Count="10" />
      <LineId Id="20" Count="0" />
    </LineIds>
    <LineIds Name="FB_Arbiter.ArbitrateBP">
      <LineId Id="19" Count="1" />
      <LineId Id="8" Count="0" />
      <LineId Id="10" Count="8" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_Arbiter.CheckRequest">
      <LineId Id="12" Count="2" />
      <LineId Id="32" Count="0" />
    </LineIds>
    <LineIds Name="FB_Arbiter.GetArbitratedBP">
      <LineId Id="6" Count="10" />
      <LineId Id="25" Count="18" />
      <LineId Id="52" Count="0" />
      <LineId Id="44" Count="0" />
      <LineId Id="53" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="51" Count="0" />
      <LineId Id="50" Count="0" />
    </LineIds>
    <LineIds Name="FB_Arbiter.RemoveRequest">
      <LineId Id="10" Count="7" />
      <LineId Id="5" Count="0" />
      <LineId Id="27" Count="0" />
      <LineId Id="26" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>